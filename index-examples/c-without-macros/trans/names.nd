module names

imports

  lib/analysis-library
  lib/analysis-library-internal
  lib/index-library
  include/C-without-macros
  desugar
  types
  
namespaces
  
  CompilationUnit
  Include
  
  Struct
  Type
  Field
  Function
  Variable

rules // Compilation unit

  Start(x, _, _) :
    defines CompilationUnit x
  	scopes Struct, Type, Function
  	
  Include(x) :
    defines non-unique Include x
    refers to CompilationUnit x
    imports Struct   from CompilationUnit x {current-file}
    imports Type     from CompilationUnit x {current-file}
    imports Function from CompilationUnit x {current-file}
  	
rules // Struct

  StructDecl(x, _) :
  	defines Struct x of type Type(x)
  	scopes Field
  	
  TypeDef(e, x) :
  	defines Type x
  	
  Type(x) :
  	refers to Type x
  	refers to Type "char"
  	refers to Type "int"
  	refers to Type "void"
  	
  StructRef(x) :
    refers to Struct x
  	
rules // Variable

  Field(t, x) :
  	defines Field x of type t
  	
  VarDecl(t, x) :
  	defines Variable x of type t in subsequent scope
  	
  Param(t, x) :
  	defines Variable x of type t
  	
  VarRef(x) :
  	refers to Variable x
  
  // Requires special lookup because of typedef loops, defined in analysis-manual.str
  Access(e, f) :
    refers to Field f
  
rules // Function

  FunDecl(t, x, _, _) :
  	defines Function x of type t
  	scopes Variable
  	
  Call(x, _) :
  	refers to Function x