module splitter
 
imports
 
  include/CSharpPartialClassses
  lib/index-library
  lib/analysis-library
  lib/analysis-library-internal
  
signature constructors
	
	Partial : ASTType
 
rules // Splitter configuration
 
  // Enable AST splitting by analyze-top for compilation of top level units.
  index-split = id
  
  // Top level units
  index-is-toplevel = ?Class(_, _)
  index-is-toplevel = ?Class(_, _, _)
  index-is-toplevel = ?PartialClass(_, _)
  index-is-toplevel = ?PartialClass(_, _, _)
  
  // Qualifiers
  index-is-qualifier = ?Namespace(_, _)
  index-is-qualifier = ?Start(_, _)
  index-qualifier-subelements = ?Namespace(_, <id>)
  index-qualifier-subelements = ?Start(_, <id>)
  index-create-qualifier(|qualifier) = !Namespace(<?Namespace(<id>, _)> qualifier, [<id>])
  index-create-qualifier(|qualifier) = !Start(<?Start(<id>, _)> qualifier, [<id>])
  
rules // Store that partial classes are partial ASTs
  
  adjust-index-def-data(store|namespace, path):
    PartialClass(x, _) -> <store> DefData([namespace|path], Partial(), Partial())
    
  adjust-index-def-data(store|namespace, path):
    PartialClass(x, _, _) -> <store> DefData([namespace|path], Partial(), Partial())
    